outputformat := "svg";
outputformatoptions := "format=rgb antialias=best";
hppp := 0.1;
vppp := 0.1;
tracingonline:=1;
%tracingmacros:=1;
outputtemplate := "%j-%c.svg";

u := 1cm;
h := u;
w := u;
space := .2h;
rounding := space;
color golden; golden := 210/256*red + 182/256*green + 120/256*blue;
color navy; navy := 1/256*red + 14/256*green + 102/256*blue;
color dark_navy; dark_navy := 16/256*red + 43/256*blue;
path square; square := ((rounding, 0)--(w - rounding, 0){right}..(w, rounding){up}--(w, h - rounding){up}..(w - rounding, h){left}--(rounding, h){left}..(0, h - rounding){down}--(0, rounding)..cycle);
pen square_pen; square_pen := makepen square;
% path bout; bout := ((w - rounding, h){left}--(rounding, h){left}..(0, h - rounding){down}--(0, rounding){down}..(rounding, 0){right}--(w - rounding, 0){right});
path bout; bout := ((0, h - rounding){down}--(0, rounding){down});
path basic_forward; basic_forward := ((rounding, 0){right}--(w - rounding, 0){right});
path basic_backward; basic_backward := ((w - rounding, h){left}--(rounding, h){left});
pair dirs[];
path the_path;

def point_dir(expr path_point, path_dir, square_coords, dir_angle) =
    path_point rotatedaround((h/2, w/2), dir_angle) shifted (square_coords xscaled (w + space) yscaled (h + space)){path_dir rotated dir_angle}
enddef;

def extremity(expr square_coords, dir_angle) =
    point_dir((0, h - rounding), down, square_coords, dir_angle)--point_dir((0, rounding), down, square_coords, dir_angle)
enddef;

def elt_forward(expr square_coords, dir_angle) =
    point_dir((rounding, 0), right, square_coords, dir_angle)--point_dir((w - rounding, 0), right, square_coords, dir_angle)
enddef;

def elt_backward(expr square_coords, dir_angle) =
    point_dir((w - rounding, h), left, square_coords, dir_angle)--point_dir((rounding, h), left, square_coords, dir_angle)
enddef;

def elt_forward_concave(expr square_coords, dir_angle) = 
    point_dir((-space, 0), right, square_coords, dir_angle)..point_dir((0, -space), right, square_coords, dir_angle)
enddef;

def elt_backward_concave(expr square_coords, dir_angle) = 
    point_dir((w + space, h), right, square_coords, dir_angle)..point_dir((w, h + space), right, square_coords, dir_angle)
enddef;

def whole_path(expr len)(text coords) =
    begingroup
        for i = 0 upto len - 2:
            angle_dirs[i] := angle (coords[i + 1] - coords[i]);
        endfor
        for i = 1 upto len - 2:
            angle_ddirs[i] := angle_dirs[i]-angle_dirs[i-1];
        endfor
        extremity(coords[0], angle_dirs[0])
            ..elt_forward(coords[0], angle_dirs[0])
            for i = 1 upto len - 2:
                if angle_ddirs[i] = 0:
                    ..elt_forward(coords[i], angle_dirs[i])
                elseif angle_ddirs[i] = 90:
                    ..elt_forward(coords[i], angle_dirs[i-1])
                    ..elt_forward(coords[i], angle_dirs[i])
%                else:
%                    ..elt_forward_concave(coords[i], angle_dirs[i-1])
                fi
            endfor
            ..elt_forward(coords[len - 1], angle_dirs[len - 2])
            ..extremity(coords[len - 1], (angle_dirs[len - 2] + 180))
            ..elt_backward(coords[len - 1], angle_dirs[len - 2])
            for i = len - 2 downto 1:
                if angle_ddirs[i] = 0:
                    ..elt_backward(coords[i], angle_dirs[i])
                elseif angle_ddirs[i] = -90:
                    ..elt_backward(coords[i], angle_dirs[i])
                    ..elt_backward(coords[i], angle_dirs[i-1])
%                else:
%                    ..elt_backward_concave(coords[i], angle_dirs[i])
                fi
            endfor
            ..elt_backward(coords[0], angle_dirs[0])
            ..cycle
    endgroup
enddef;


beginfig(1);
fill unitsquare xscaled 10w yscaled 5h shifted (-space, -space) withcolor dark_navy;
for i_row = 0 upto 3:
    for j_col = 0 upto 3:
        fill square shifted (i_row*w + (i_row)*space, j_col*h + j_col*space) withcolor golden;
    endfor
endfor
endfig;

beginfig(2);
fill unitsquare xscaled 10w yscaled 5h shifted (-space, -space) withcolor dark_navy;
pair A; A := (0, 3(h + space));
pair B; B := (0, 2(h + space));
fill ((A + (0, h - rounding)){down}--(B + (0, rounding)){down}..(B + (rounding, 0){right})--(B + (w -rounding, 0){right})..(B + (w, rounding)){up}--(A + (w, h - rounding)){up}..(A + (w - rounding, h)){left}--(A + (rounding, h)){left}..cycle) withcolor golden;
endfig;

beginfig(3);
fill unitsquare xscaled 10w yscaled 5h shifted (-space, -space) withcolor dark_navy;
endfig;

beginfig(4);
fill unitsquare xscaled 10w yscaled 5h shifted (-space, -space) withcolor dark_navy;
    pair coords[];
    coords[0] = (0,0);
    coords[1] = (1,0);
    coords[2] = (1,1);
    coords[3] = (2,1);
    fill whole_path(4)(coords) withcolor golden;
endfig;


beginfig(5);
fill unitsquare xscaled 10w yscaled 5h shifted (-space, -space) withcolor dark_navy;
    pair coords[];
    coords[0] := (0,3);
    coords[1] := (0,2);
    fill whole_path(2)(coords) withcolor golden;
    pair a[];
    a[0] := (1,3);
    a[1] := (1,2);
    fill whole_path(2)(a) withcolor golden;
    pair b[];
    b[0] := (0,1);
    b[1] := (0,0);
    b[2] := (1,0);
    b[3] := (2,0);
    fill whole_path(4)(b) withcolor golden;
    pair c[];
    c[0] := (1,1);
    c[1] := (2,1);
    c[2] := (2,2);
    fill whole_path(3)(c) withcolor golden;
    pair d[];
    d[0] := (2,3);
    d[1] := (3,3);
    fill whole_path(2)(d) withcolor golden;
    pair e[];
    e[0] := (3,0);
    e[1] := (3,1);
    e[2] := (3,2);
    fill whole_path(3)(e) withcolor golden;

endfig;
end;
